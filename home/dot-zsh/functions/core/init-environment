#!/bin/zsh

#
# Options
#

typeset -ga zopts_general=(
  COMBINING_CHARS       # Combine 0-len chars with base chars (eg: accents).
  INTERACTIVE_COMMENTS  # Enable comments in interactive shell.
  RC_QUOTES             # Allow 'Hitchhiker''s Guide' instead of 'Hitchhiker'\''s Guide'.
  NO_MAIL_WARNING       # Don't print a warning message if a mail file has been accessed.
)

typeset -ga zopts_jobs=(
  LONG_LIST_JOBS       # List jobs in the long format by default.
  AUTO_RESUME          # Attempt to resume existing job before creating a new process.
  NOTIFY               # Report status of background jobs immediately.
  NO_BG_NICE           # Don't run all background jobs at a lower priority.
  NO_HUP               # Don't kill jobs on shell exit.
  NO_CHECK_JOBS        # Don't report on jobs when shell exit.
)

setopt $zopts_general $zopts_jobs


#
# XDG base dirs
#

export XDG_CONFIG_HOME=${XDG_CONFIG_HOME:-~/.config}
export XDG_CACHE_HOME=${XDG_CACHE_HOME:-~/.cache}
export XDG_DATA_HOME=${XDG_DATA_HOME:-~/.local/share}
export XDG_RUNTIME_DIR=${XDG_RUNTIME_DIR:-~/.xdg}
for xdir in $XDG_CONFIG_HOME $XDG_CACHE_HOME $XDG_DATA_HOME $XDG_RUNTIME_DIR; do
  [[ -d $xdir ]] || mkdir -p $xdir
done
unset xdir

#
# Editors
#

export EDITOR=${EDITOR:-vim}
export VISUAL=${VISUAL:-vim}
export PAGER=${PAGER:-less}

#
# Path
#

# Ensure path arrays do not contain duplicates.
typeset -gU path fpath cdpath

path=(
  $HOME/{,s}bin(N)
  /opt/{homebrew,local}/{,s}bin(N)
  /usr/local/{,s}bin(N)
  $path
)

#
# Less
#

export LESS=${LESS:-'-g -i -M -R -S -w -z-4'}

#
# Browser
#

if [[ "$OSTYPE" == darwin* ]]; then
  export BROWSER=${BROWSER:-open}
fi

#
# Misc
#

# use `< file` to quickly view the contents of any file.
export READNULLCMD=${READNULLCMD:-$PAGER}
