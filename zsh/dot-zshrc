#!/bin/zsh

# load zprof first if we need to profile
[[ ${ZPROFRC:-0} -eq 0 ]] || zmodload zsh/zprof
alias zprofrc="ZPROFRC=1 zsh"

# Use Zebrafish to drive config
source $ZDOTDIR/.zebrafish

# Add zfuncdir sub directories.
ZFUNCDIR=$ZDOTDIR/functions
for _subdir in $ZFUNCDIR/*(/N); do
  if [[ "$_subdir:t" == macos ]] && [[ "$OSTYPE" != darwin* ]]; then
    continue
  fi
  fpath=($_subdir $fpath)
  autoload -Uz $fpath[1]/*(.:t)
done

#region aliases

# mask built-ins with better defaults
alias mkdir='mkdir -p'
alias ping='ping -c 5'
alias type='type -a'
alias vi=vim
if [[ "$OSTYPE" == darwin* ]]; then
  alias ls="ls -G"
else
  alias ls="ls --group-directories-first --color=auto"
fi
alias grep="grep --color=auto --exclude-dir={.git,.svn}"

# more ways to ls
alias ll='ls -lh'
alias la='ls -lAh'
alias ldot='ls -ld .*'

# fix typos
alias quit='exit'
alias cd..='cd ..'
alias zz='exit'

# tar sux
alias tarls="tar -tvf"
alias untar="tar -xf"

#endregion

#region utils

# macOS commands everywhere
if [[ "$OSTYPE" == darwin* ]]; then
  # canonical hex dump
  (( $+commands[hd] )) || alias hd="hexdump -C"

  # macOS has no 'md5sum', so use 'md5' as a fallback
  (( $+commands[md5sum] )) || alias md5sum="md5"

  # macOS has no 'sha1sum', so use 'shasum' as a fallback
  (( $+commands[sha1sum] )) || alias sha1sum="shasum"

elif [[ "$OSTYPE" == cygwin* ]]; then
  alias open='cygstart'
  alias pbcopy='tee > /dev/clipboard'
  alias pbpaste='cat /dev/clipboard'
elif [[ "$OSTYPE" == linux-android ]]; then
  alias open='termux-open'
  alias pbcopy='termux-clipboard-set'
  alias pbpaste='termux-clipboard-get'
else
  alias open='xdg-open'
  if [[ -n $DISPLAY ]]; then
    if (( $+commands[xclip] )); then
      alias pbcopy='xclip -selection clipboard -in'
      alias pbpaste='xclip -selection clipboard -out'
    elif (( $+commands[xsel] )); then
      alias pbcopy='xsel --clipboard --input'
      alias pbpaste='xsel --clipboard --output'
    fi
  else
    if (( $+commands[wl-copy] && $+commands[wl-paste] )); then
      alias pbcopy='wl-copy'
      alias pbpaste='wl-paste'
    fi
  fi
fi

# Set initial working directory.
IWD=${IWD:-$PWD}
alias iwd='cd $IWD'

#endregion

#region plugins

if (( $+functions[plugin-clone] )); then
  ZPLUGINDIR=$ZDOTDIR/plugins
  repos=(
    mattmc3/zman
    ohmyzsh/ohmyzsh
    romkatv/gitstatus
    romkatv/zsh-bench
    romkatv/zsh-defer
    sindresorhus/pure
    zdharma-continuum/fast-syntax-highlighting
    zsh-users/zsh-autosuggestions
    zsh-users/zsh-completions
    zsh-users/zsh-history-substring-search
  )
  plugin-clone $repos

  omz_plugins=(
    extract
    fancy-ctrl-z
    magic-enter
  )
  plugin-source --dir $ZDOTDIR/plugins/ohmyzsh/plugins $omz_plugins

  plugins=(
    zman
    pure
    zsh-autosuggestions
    zsh-history-substring-search
    zsh-defer
    fast-syntax-highlighting
  )
  plugin-source $plugins

  path+=($ZPLUGINDIR/zsh-bench)

  MAGIC_ENTER_GIT_COMMAND="ls; git status -sb"
  MAGIC_ENTER_OTHER_COMMAND="ls"
fi

#endregion

# local overrides
[[ ! -f $DOTFILES.local/zsh/zshrc_local.zsh ]] || source $DOTFILES.local/zsh/zshrc_local.zsh

# done profiling
[[ ${ZPROFRC:-0} -eq 0 ]] || { unset ZPROFRC && zprof }
